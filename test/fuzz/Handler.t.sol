//SPDX-License-Identifier: MIT

pragma solidity ^0.8.18;

import {Test, console} from "forge-std/Test.sol";
import {DSCEngine} from "../../src/DSCEngine.sol";
import {DecentralizedStableCoin} from "../../src/DecentralizedStableCoin.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

contract Handler is Test {
    DSCEngine public s_dscEngine;
    DecentralizedStableCoin public s_dsc;
    address public weth;
    address public wbtc;
    uint256 MAX_DEPOSIT_SIZE = type(uint96).max;
    uint256 public timesMintDscCalled = 0;
    address[] public addressWithCollateralDeposited;

    constructor(DSCEngine dscEngine, DecentralizedStableCoin dsc) {
        s_dscEngine = dscEngine;
        s_dsc = dsc;
        address[] memory collateralTokens = s_dscEngine.getCollateralTokens();
        weth = collateralTokens[0];
        wbtc = collateralTokens[1];
    }

    function mintDsc(uint256 amountDsc, uint256 addressCollateralSeed) public {
        if (addressWithCollateralDeposited.length == 0) {
            return; // No addresses with collateral deposited
        }
        address sender = addressWithCollateralDeposited[addressCollateralSeed % addressWithCollateralDeposited.length];
        vm.startPrank(sender);
        (uint256 totalDscMinted, uint256 totalCollateralValue) = s_dscEngine.getAccountInformation(sender);
        int256 maxDscToMint = (int256(totalCollateralValue) / 3) - int256(totalDscMinted); // 50% collateralization ratio
        if (maxDscToMint < 0) {
            vm.stopPrank(); // Ensure prank is stopped if we return early
            return;
        }
        amountDsc = bound(amountDsc, 0, uint256(maxDscToMint));
        if (amountDsc == 0) {
            vm.stopPrank(); // Ensure prank is stopped if we return early
            return;
        }
        console.log("Total Collateral Value: ", totalCollateralValue);
        console.log("Minting DSC: ", amountDsc);
        s_dscEngine.mintDsc(amountDsc);
        vm.stopPrank();
        timesMintDscCalled++;
    }

    function depositCollateral(uint256 collateralSeed, uint256 amountCollateral) public {
        address collateralAddress = _getCollateralFromSeed(collateralSeed);
        amountCollateral = bound(amountCollateral, 1, MAX_DEPOSIT_SIZE);
        // Ensure amountCollateral is not excessively large for practical testing if needed,
        // or rely on DSCEngine's own checks.
        // For fuzzing, we might want to cap it or let DSCEngine handle invalid amounts.
        // For now, let's assume amountCollateral is a reasonable value generated by the fuzzer.

        // If amountCollateral is 0, DSCEngine's moreThanZero modifier will catch it.
        // We only need to mint and approve if amountCollateral > 0.
        vm.startPrank(msg.sender);
        ERC20Mock(collateralAddress).mint(msg.sender, amountCollateral);
        ERC20Mock(collateralAddress).approve(address(s_dscEngine), amountCollateral);
        console.log("Depositing Collateral: ", amountCollateral);
        s_dscEngine.depositCollateral(collateralAddress, amountCollateral);
        vm.stopPrank();
        addressWithCollateralDeposited.push(msg.sender);
    }

    function redeemCollateral(uint256 collateralSeed, uint256 amountCollateral) public {
        address collateralAddress = _getCollateralFromSeed(collateralSeed);

        // We need to prank as the EOA that originally deposited the collateral.
        // The fuzzer sets msg.sender for calls to Handler's public functions.
        vm.startPrank(msg.sender);

        uint256 maxCollateralToRedeem = s_dscEngine.getCollateralBalanceOfUser(msg.sender, collateralAddress);
        amountCollateral = bound(amountCollateral, 0, maxCollateralToRedeem / 3);

        if (amountCollateral == 0) {
            vm.stopPrank(); // Ensure prank is stopped if we return early
            return;
        }
        console.log("Redeeming Collateral: ", amountCollateral);
        s_dscEngine.redeemCollateral(collateralAddress, amountCollateral);
        vm.stopPrank();
    }

    function _getCollateralFromSeed(uint256 collateralSeed) private view returns (address) {
        if (collateralSeed % 2 == 0) {
            return weth;
        }
        return wbtc;
    }
}
